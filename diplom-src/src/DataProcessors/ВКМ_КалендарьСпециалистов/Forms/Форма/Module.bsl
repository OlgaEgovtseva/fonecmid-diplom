
#Область ОбработчикиСобытийФормы

// Код процедур и функций


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	
	НачалоПериода = НачалоНедели(ТекущаяДатаСеанса());
	КонецПериода = КонецНедели(ТекущаяДатаСеанса());
	
	Планировщик.ТекущиеПериодыОтображения.Добавить(НачалоПериода, КонецПериода);
	
	ОбновитьПланировщик();
	

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ВКМ_ОбслуживаниеКлиентов" Тогда 
		ОбновитьПланировщик();
	КонецЕсли;
	

КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Код процедур и функций


&НаКлиенте
Процедура ПланировщикПередСозданием(Элемент, Начало, Конец, ЗначенияИзмерений, Текст, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ЗначенияЗаполнения = Новый Структура; 
	//@skip-check use-non-recommended-method
	ЗначенияЗаполнения.Вставить("Дата", ТекущаяДата()); 
	ЗначенияЗаполнения.Вставить("ДатаПроведенияРабот", НачалоДня(Начало));
	ЗначенияЗаполнения.Вставить("ВремяНачалаРабот_план", Дата(1,1,1) + (Начало - НачалоДня(Начало))); 
	ЗначенияЗаполнения.Вставить("ВремяОкончанияРабот_план", Дата(1,1,1) + (Конец - НачалоДня(Конец)));
	ЗначенияЗаполнения.Вставить("Специалист", ПользователиКлиент.ТекущийПользователь());
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ОткрытьФорму("Документ.ВКМ_ОбслуживаниеКлиентов.ФормаОбъекта", ПараметрыФормы);

КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПриАктивизации(Элемент)
	ДокСсылка = Элемент.ВыделенныеЭлементы[0].Значение; 
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", ДокСсылка);
	
	ОткрытьФорму("Документ.ВКМ_ОбслуживаниеКлиентов.Форма.ФормаДокумента", ПараметрыФормы);

КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Обновить(Команда)
	ОбновитьПланировщик();
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьПланировщик()
	
	НачалоПериода = НачалоНедели(ТекущаяДатаСеанса());
	КонецПериода = КонецНедели(ТекущаяДатаСеанса());
	
	Месяц = 2628000;
	
	Планировщик.Элементы.Очистить();
	
	Заявки = Документы.ВКМ_ОбслуживаниеКлиентов.Выбрать(НачалоПериода - Месяц, КонецПериода + Месяц);
	
	Пока Заявки.Следующий() Цикл 
		
		НачалоЗаявки = Заявки.ДатаПроведенияРабот + (Заявки.ВремяНачалаРабот_план - Дата(1,1,1));
		КонецЗаявки = Заявки.ДатаПроведенияРабот + (Заявки.ВремяОкончанияРабот_план - Дата(1,1,1));
		
		ЭлементПланировщика = Планировщик.Элементы.Добавить(НачалоЗаявки, КонецЗаявки);
		ЭлементПланировщика.Значение = Заявки.Ссылка;
		ЭлементПланировщика.Текст = Заявки.Специалист; 
		
	КонецЦикла; 
	
КонецПроцедуры


#КонецОбласти
