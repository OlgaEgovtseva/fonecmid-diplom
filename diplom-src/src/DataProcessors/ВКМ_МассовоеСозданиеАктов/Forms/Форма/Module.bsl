
#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПериодНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("Обработка.ВКМ_МассовоеСозданиеАктов.Форма.ВыборСтандартногоПериодаМесяц", , Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПериодОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Объект.НачалоПериода = ВыбранноеЗначение.НачалоПериода;
	Объект.КонецПериода = ВыбранноеЗначение.КонецПериода;
	Объект.Период = Формат(ВыбранноеЗначение.НачалоПериода, "ДФ='ММMM yyyy'");
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы

// Код процедур и функций


&НаКлиенте
Процедура СоздатьАкты(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.Период) Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю("Выберите период");
		Возврат;
	КонецЕсли;
	
	ДлительнаяОперация = СоздатьАктыНаСервере();
	
	Оповещение = Новый ОписаниеОповещения("СоздатьАктыЗавершение", ЭтотОбъект);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Оповещение); 
	
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция СоздатьАктыНаСервере()
	
	ИмяМетода = "Обработки.ВКМ_МассовоеСозданиеАктов.СоздатьАктыНаСервере";
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(УникальныйИдентификатор, ИмяМетода, Объект.НачалоПериода, Объект.КонецПериода);

КонецФункции

&НаКлиенте
Процедура СоздатьАктыЗавершение(Результат, ДополнительныеПараметры) Экспорт 
	
	МассивДляТЧ = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	ДобавитьСтрокувВТЧОбработки(МассивДляТЧ);
	ОбщегоНазначенияКлиент.СообщитьПользователю("Обработка завершена");
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьСтрокувВТЧОбработки(МассивДляТЧ)
	
	ТабЧасть = Объект.ДоговорыИРеализации;
	ТабЧасть.Очистить();
	
	Если МассивДляТЧ <> Неопределено Тогда
		Для Каждого Элемент Из МассивДляТЧ Цикл 
			
			Обработки.ВКМ_МассовоеСозданиеАктов.ДобавитьСтрокуВТЧ(Элемент.Договор, Элемент.Реализация, ТабЧасть); 
			
		КонецЦикла; 
	КонецЕсли;

КонецПроцедуры


#КонецОбласти
