#Область ПрограммныйИнтерфейс

//Отправить уведомления в телеграм
Процедура ОтправитьУведомлениеВТелеграм() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВКМ_УведомленияТелеграм_БотуДляОтправки.Ссылка,
	|	ВКМ_УведомленияТелеграм_БотуДляОтправки.ТекстСообщения
	|ИЗ
	|	Справочник.ВКМ_УведомленияТелеграм_БотуДляОтправки КАК ВКМ_УведомленияТелеграм_БотуДляОтправки";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		
		Ответ = ОповеститьСпециалистов(Выборка.ТекстСообщения);
		Если Ответ.КодСостояния = 200 Тогда 
			Выборка.Ссылка.ПолучитьОбъект().Удалить();
		КонецЕсли;
		
	КонецЦикла;
			
КонецПроцедуры


//Оповестить специалистов об изменении документа
//Параметры:
//	ТекстСообщения - Строка - реквизит спрвочника
//Возвращаемое значение: 
//	HTTPОтвет
Функция ОповеститьСпециалистов(ТекстСообщения) Экспорт
	Токен = Константы.ВКМ_ТокенУправленияТелеграм_Ботом.Получить();
	ИДЧата = Константы.ВКМ_ИдентификаторГруппыДляОповещения.Получить();
	АдресРесурса = "/bot" + Токен + "/sendMessage"; 
	
    Соединение = Новый HTTPСоединение("api.telegram.org", 443,,,,, Новый ЗащищенноеСоединениеOpenSSL);
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/json");
	
	Запрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("chat_id", ИДЧата);
	СтруктураДанных.Вставить("text", ТекстСообщения);
	
	ПараметрыЗаписи = Новый ПараметрыЗаписиJSON(, Символы.Таб);
	
	Запись = Новый ЗаписьJSON;
	Запись.УстановитьСтроку(ПараметрыЗаписи);
	ЗаписатьJSON(Запись, СтруктураДанных);
	
	СтрокаJSON = Запись.Закрыть();
	
	Запрос.УстановитьТелоИзСтроки(СтрокаJSON);
	
	Результат = Соединение.ВызватьHTTPМетод("POST", Запрос);
	
	Если Результат.КодСостояния <> 200 Тогда 
		ЗаписьЖурналаРегистрации("Ошибка", УровеньЖурналаРегистрации.Ошибка,,,Результат.ПолучитьТелоКакСтроку());
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

#КонецОбласти
