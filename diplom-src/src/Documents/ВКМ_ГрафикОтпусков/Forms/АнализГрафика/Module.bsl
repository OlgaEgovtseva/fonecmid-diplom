
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	//@skip-check unknown-form-parameter-access
	ОтпускаСотрудников.Загрузить(ПолучитьИзВременногоХранилища(Параметры.Объект));
	//@skip-check unknown-form-parameter-access
	Год = ПолучитьИзВременногоХранилища(Параметры.Год);

	ДиаграммаГанта.Обновление = Ложь;
	ДиаграммаГанта.АвтоОпределениеПолногоИнтервала = Ложь;
	ДиаграммаГанта.Очистить();
	
	ДиаграммаГанта.УстановитьПолныйИнтервал(НачалоГода(Год), КонецГода(Год));
	Серия = ДиаграммаГанта.УстановитьСерию("Отпуска");
		
	Для Каждого Строка Из ОтпускаСотрудников Цикл 
		
		Точка = ДиаграммаГанта.УстановитьТочку(Строка.Сотрудник);
		Точка.Картинка = БиблиотекаКартинок.СостояниеПользователя02;
		Значение = ДиаграммаГанта.ПолучитьЗначение(Точка, Серия);
		Значение.Данные.ДнейОтпуска = Значение.Данные.ДнейОтпуска + Строка.КоличествоДнейОтпуска;
		Если Значение.Данные.ДнейОтпуска > 28 Тогда 
			Значение.ЦветФона = WebЦвета.ВесеннеЗеленый;
		КонецЕсли;
				
		Интервал = Значение.Добавить();
		Интервал.Начало = Строка.ДатаНачала;
		Интервал.Конец = Строка.ДатаОкончания;
		
	КонецЦикла;
	
	Шкала = ДиаграммаГанта.ОбластьПостроения.ШкалаВремени.Элементы[0];
	Шкала.Метки.Очистить();
	//@skip-check use-non-recommended-method
	МеткиВремени = Шкала.Метки.Добавить(НачалоДня(ТекущаяДата())); 
	//@skip-check use-non-recommended-method
	МеткиВремени.Текст = "Текущая дата " + Формат(ТекущаяДата(), "ДФ=dd.MM.yyyy");
	МеткиВремени.ЦветТекста = WebЦвета.Красный;
	МеткиВремени.ЦветЛинии = WebЦвета.Красный; 
	
	ДиаграммаГанта.Обновление = Истина;
	
КонецПроцедуры
	
#КонецОбласти