
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.РеализацияТоваровУслуг) Тогда
		
        КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
        КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.РеализацияТоваровУслуг.ПолноеИмя();
        КомандаСоздатьНаОсновании.Представление = ОбщегоНазначения.ПредставлениеОбъекта(Метаданные.Документы.РеализацияТоваровУслуг);
        КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;

	Возврат Неопределено;
	
КонецФункции

Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Акт об оказанных услугах
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АктОбОказанныхУслугах";
	КомандаПечати.Представление = НСтр("ru = 'Акт об оказанных услугах'");
	КомандаПечати.Порядок = 1;
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт

	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "АктОбОказанныхУслугах");
	Если ПечатнаяФорма <> Неопределено Тогда
	    ПечатнаяФорма.ТабличныйДокумент = ВКМ_АктОбОказанныхУслугах(МассивОбъектов, ОбъектыПечати);
	    ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Акт об оказанных услугах'");
	    ПечатнаяФорма.ПолныйПутьКМакету = "Документ.РеализацияТоваровУслуг.ВКМ_ПФ_MXL_АктОбОказанныхУслугах";
	КонецЕсли;

КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

Функция ВКМ_АктОбОказанныхУслугах(МассивОбъектов, ОбъектыПечати)

	ТабДок = Новый ТабличныйДокумент;
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.РеализацияТоваровУслуг.ВКМ_ПФ_MXL_АктОбОказанныхУслугах");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка,
	|	РеализацияТоваровУслуг.Номер,
	|	РеализацияТоваровУслуг.Дата,
	|	РеализацияТоваровУслуг.Контрагент,
	|	РеализацияТоваровУслуг.Организация,
	|	РеализацияТоваровУслуг.Договор,
	|	РеализацияТоваровУслуг.Услуги.(
	|		Номенклатура,
	|		Количество,
	|		Цена,
	|		Сумма) КАК Услуги
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка В (&МассивОбъектов)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Выборка = Запрос.Выполнить().Выбрать();
	
	ОбластьШапка = Макет.ПолучитьОбласть("ОбластьШапка");
	ОбластьСтрокаТаблицы = Макет.ПолучитьОбласть("ОбластьСтрокаТаблицы");  
	ОбластьПодвал = Макет.ПолучитьОбласть("ОбластьПодвал");
	
	ТабДок.Очистить();
	
	ВставлятьРазделительСтраниц = Ложь;
	
	Пока Выборка.Следующий() Цикл
		
		Если ВставлятьРазделительСтраниц Тогда 
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ТабДок.ВысотаТаблицы + 1;
		
		ОбластьШапка.Параметры.НомерАкта = Формат(Число(Выборка.Номер), "ЧГ=;");
		ОбластьШапка.Параметры.ДатаАкта = Формат(Выборка.Дата, "ДФ='dd.MM.yyyy ''г.''';");
		ОбластьШапка.Параметры.Организация = Выборка.Организация;
		ОбластьШапка.Параметры.Контрагент = Выборка.Контрагент;
		ОбластьШапка.Параметры.Договор = Выборка.Договор;
		
		ТабДок.Вывести(ОбластьШапка);
		
		ВыборкаУслуги = Выборка.Услуги.Выбрать();
		
		НомерСтроки = 1;
		СуммаИтого = 0;
		
		Пока ВыборкаУслуги.Следующий() Цикл
			
			ОбластьСтрокаТаблицы.Параметры.НомерСтроки = НомерСтроки;
			НомерСтроки = НомерСтроки + 1;
			ОбластьСтрокаТаблицы.Параметры.Услуга = ВыборкаУслуги.Номенклатура;
			ОбластьСтрокаТаблицы.Параметры.Количество = ВыборкаУслуги.Количество;
			ОбластьСтрокаТаблицы.Параметры.Цена = ВыборкаУслуги.Цена;
			ОбластьСтрокаТаблицы.Параметры.Сумма = ВыборкаУслуги.Сумма;
			СуммаИтого = СуммаИтого + ВыборкаУслуги.Сумма;
			
			ТабДок.Вывести(ОбластьСтрокаТаблицы);
			
		КонецЦикла;
		
		ОбластьПодвал.Параметры.СуммаИтого = СуммаИтого;
		ОбластьПодвал.Параметры.СуммаПрописью = ЧислоПрописью(СуммаИтого, "Л=ru_RU;ДП=Истина", "рубль,рубля,рублей,м,копейка,копейки,копеек,ж,2");
		
		ТабДок.Вывести(ОбластьПодвал);
		
		ВставлятьРазделительСтраниц = Истина;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДок, НомерСтрокиНачало,
			ОбъектыПечати, Выборка.Ссылка);

	КонецЦикла;
	
	Возврат ТабДок;


КонецФункции

#КонецОбласти

#КонецЕсли