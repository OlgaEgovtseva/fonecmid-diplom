
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ,Режим)
	
	Если Не ДоговорДействителен() Тогда
		ОбщегоНазначения.СообщитьПользователю("Проверьте атуальность или тип договора");
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	СтоимостьЧасаРаботы = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Договор, "ВКМ_СтоимостьЧасаРаботы"); 
	
	// регистр ВКМ_ВыполненныеКлиентуРаботы
	Движения.ВКМ_ВыполненныеКлиентуРаботы.Записывать = Истина;
	Движение = Движения.ВКМ_ВыполненныеКлиентуРаботы.Добавить();
	Движение.Период = Дата;
	Движение.Клиент = Клиент;
	Движение.Договор = Договор;	
	ЧасыКОплатеКлиенту = ВыполненныеРаботы.Итог("ЧасыКОплатеКлиенту");
	Движение.КоличествоЧасов = ЧасыКОплатеКлиенту;
	Движение.СуммаКОплате = ЧасыКОплатеКлиенту * СтоимостьЧасаРаботы;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.ПроцентОтРабот
		|ИЗ
		|	РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(&Дата, Сотрудник = &Сотрудник) КАК
		|		ВКМ_УсловияОплатыСотрудниковСрезПоследних";
	
	Запрос.УстановитьПараметр("Сотрудник", Специалист);
	Запрос.УстановитьПараметр("Дата", Дата);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ПроцентОтРабот = Выборка.ПроцентОтРабот;
	Иначе
		Отказ = Истина;
		Возврат;
	КонецЕсли;
		
	// регистр ВКМ_ВыполненныеСотрудникомРаботы 
	Движения.ВКМ_ВыполненныеСотрудникомРаботы.Записывать = Истина;
	Движение = Движения.ВКМ_ВыполненныеСотрудникомРаботы.Добавить();
	Движение.Период = Дата;
	Движение.Сотрудник = Специалист;
	Движение.ЧасовКОплате = ЧасыКОплатеКлиенту;
	Движение.СуммаКОплате = ЧасыКОплатеКлиенту * СтоимостьЧасаРаботы * ПроцентОтРабот / 100;
	
КонецПроцедуры	

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДоговорДействителен()
	
	ДанныеДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Договор, "ВКМ_ДействуетС, ВКМ_ДействуетПо, ВидДоговора");
	
	ДействуетС = ДанныеДоговора.ВКМ_ДействуетС;
	ДействуетПо = ДанныеДоговора.ВКМ_ДействуетПо;
	ВидДоговора = ДанныеДоговора.ВидДоговора;
	
	Если Не ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание ИЛИ НЕ (Дата >= ДействуетС И Дата <= ДействуетПо) Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецЕсли
